apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'de.mannodermaus.android-junit5'
apply from: '../fmp_settings.gradle'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

def prefixAppVersion = "7"

android {
    compileSdkVersion build_versions.target_sdk
    defaultConfig {
        applicationId "com.lenta.bp7"
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode getCodeVersion()
        versionName getNameVersion(prefixAppVersion)
        println 'version of' + applicationId + ": " + getNameVersion()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures{
        dataBinding = true
    }

    flavorDimensions "server"

    productFlavors {
        staging {
            dimension "server"
            stagingSettings.each {
                resValue it.type, it.name, it.value
            }
        }

        prod {
            dimension "server"
            prodSettings.each {
                resValue it.type, it.name, it.value
            }
        }

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    // For Kotlin projects
    kotlinOptions {
        jvmTarget = "1.8"
    }

    applicationVariants.all { variant ->
        if (variant.buildType.name == "release") {
            variant.outputs.all {
                def name = outputFileName.replace("app-", "LentaPlanogram-")
                outputFileName = name.replace(".apk", "-${variant.versionName}-${variant.versionCode}.apk")
            }
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    packagingOptions {
        pickFirst 'META-INF/kotlinx-io.kotlin_module'
        pickFirst 'META-INF/atomicfu.kotlin_module'
        pickFirst 'META-INF/kotlinx-coroutines-io.kotlin_module'
    }

}

dependencies {
    implementation project(':Shared')
    implementation 'com.android.support:multidex:1.0.3'

    // Simple XML
    implementation ('org.simpleframework:simple-xml:2.7.1'){
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'xpp3', module: 'xpp3'
    }

    // Dagger2
    kapt deps.dagger.compiler

    testImplementation deps.junit
    /*androidTestImplementation 'androidx.test:runner:1.2.0-alpha03'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0-alpha03'*/

    // JUnit 5 tests
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.3.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.3.2"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.3.2"

    // Support for older JUnit versions
    testImplementation deps.junit
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:5.3.2"

    // Mockito
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0"
    testImplementation "io.mockk:mockk:1.9.3"

    // Firebase
    implementation deps.firebase

    // Add the Firebase Crashlytics SDK.
    implementation deps.crashlytics

}
repositories {
    maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    mavenCentral()
}
